buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'info.solidsoft.pitest' version '1.5.2'
}

apply plugin: 'org.junit.platform.gradle.plugin'

group 'br.com.rnaufal'
version '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_13
    targetCompatibility = JavaVersion.VERSION_13
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

ext {
    junitJupiterVersion = '5.5.2'
}

jacoco {
    toolVersion = "0.8.6"
    applyTo junitPlatformTest
}

junitPlatformTest {
    jacoco {
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }
        }
    }
}

pitest {
    junit5PluginVersion = '0.12'
}

tasks.coveralls {
    dependsOn 'check'
}

jacocoTestCoverageVerification.dependsOn jacocoTestReport

check.dependsOn jacocoTestCoverageVerification

build.dependsOn 'pitest'

dependencies {
    implementation "org.apache.commons:commons-lang3:3.9"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.assertj:assertj-core:3.11.1"
}
